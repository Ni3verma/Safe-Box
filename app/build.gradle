plugins {
    alias(libs.plugins.google.services)
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.hilt)
    alias(libs.plugins.ksp)
    alias(libs.plugins.crashlytics)
}

ksp {
    arg "room.schemaLocation", "${projectDir}/schemas"
    arg "room.incremental", "true"
    arg "room.expandProjection", "true"
}

// version code will be incremental unique github run runner and default to a high value for local builds
def version_code = 1

// actual version_name(from CI) follows the pattern :majorVersion.minorVersion.dbVersion.fixVersion
// so our tag is like v2.0.4.0
def githubRef = System.getenv("GITHUB_REF_NAME") // e.g., 'v2.0.4.0' or 'master'

android {
    namespace 'com.andryoga.safebox'
    compileSdk = 36

    defaultConfig {
        applicationId "com.andryoga.safebox"
        minSdk 24
        targetSdk 36
        versionCode version_code

        // versionName logic
        if (githubRef != null && githubRef.startsWith("v")) {
            versionName githubRef.substring(1) // Strips 'v' from 'v2.0.4.0'
        } else if (githubRef != null) {
            versionName "${githubRef}-build.${version_code}" // e.g., master-build.145
        } else {
            versionName "LOCAL-build"
        }
        println "Building SafeBox: ${versionName} (${versionCode})"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            // Load releaseKeyStore.properties in an object
            // if you want to make release build in your local then make a releaseKeyStore.properties file with below keys at root level
            // also store your release key store (*.jks) file and mention path in STORE_FILE property
            // both these files are meant to be stored only locally and not committed to VCS
            def localReleaseKeyStoreProperties = new Properties()
            localReleaseKeyStoreProperties.load(new FileInputStream(rootProject.file("releaseKeyStore.properties")))
            storeFile file(localReleaseKeyStoreProperties['STORE_FILE'])
            storePassword localReleaseKeyStoreProperties['STORE_PASSWORD']
            keyPassword localReleaseKeyStoreProperties['KEY_PASSWORD']
            keyAlias localReleaseKeyStoreProperties['KEY_ALIAS']
        }
        qa {
            def nonProdKeyStoreProperties = new Properties()
            nonProdKeyStoreProperties.load(new FileInputStream(rootProject.file("nonProdReleaseKeyStore.properties")))
            storeFile file(nonProdKeyStoreProperties['STORE_FILE'])
            storePassword nonProdKeyStoreProperties['STORE_PASSWORD']
            keyPassword nonProdKeyStoreProperties['KEY_PASSWORD']
            keyAlias nonProdKeyStoreProperties['KEY_ALIAS']
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            // if you are changing apk name then also change name in git workflow file
            outputFileName = "SafeBox-${variant.name}.apk"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            firebaseCrashlytics {
                mappingFileUploadEnabled true
                nativeSymbolUploadEnabled true
            }
            ndk {
                debugSymbolLevel = "FULL"
            }
        }
        qa {
            initWith(buildTypes["release"])
            applicationIdSuffix ".qa"
            signingConfig signingConfigs.qa
            firebaseCrashlytics {
                mappingFileUploadEnabled true
                nativeSymbolUploadEnabled true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    sourceSets {
        named("androidTest") {
            assets.srcDirs(files("$projectDir/schemas"))
        }
    }
    ndkVersion '29.0.142068651'
}

dependencies {
    // test deps
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.bundles.test

    // debug deps
    debugImplementation libs.bundles.compose.debug

    implementation libs.bundles.androidx
    implementation libs.bundles.work
    implementation libs.timber
    implementation libs.security.crypto
    implementation libs.kotlinx.serialization.json
    implementation libs.biometric
    implementation libs.bundles.play.review
    implementation libs.data.store.pref

    ksp(libs.room.compiler)
    implementation libs.bundles.room

    ksp(libs.hilt.compiler)
    implementation libs.bundles.hilt

    implementation platform(libs.androidx.compose.bom)
    implementation libs.bundles.compose.ui

    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase
}