pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "com.gradle.develocity" version "4.3.1"
}

develocity {
    projectId = "safeBox"
    buildScan {
        termsOfUseUrl = 'https://gradle.com/help/legal-terms-of-use'
        termsOfUseAgree = 'yes'

        publishing {
            // Only publish if it's CI AND the build failed
            onlyIf { result ->
                boolean isCi = System.getenv("CI") == "true"
                boolean hasFailed = !result.buildResult.failures.isEmpty()

                print("develocity publishing condition check: isCi: $isCi, hasFailed: $hasFailed")

                return isCi && hasFailed
            }
        }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

include ':app'
rootProject.name = "Safe Box"